buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "fernandoperez.lifemanager"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support'
        exclude module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
        exclude group: 'com.google.guava'
    })

    // Handling a lot of dependencies. (64K-limit).
    compile ('com.android.support:multidex:1.0.1') {
        exclude group: 'com.android.support'
        exclude module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
        exclude group: 'com.google.guava'
    }

    // Google Services.
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    compile('com.google.apis:google-api-services-gmail:v1-rev57-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    compile ('com.google.android.gms:play-services-drive:10.0.1') {
        exclude group: 'com.google.guava'
    }
    compile ('com.google.android.gms:play-services-auth:10.0.1') {
        exclude group: 'com.google.guava'
    }
    compile 'pub.devrel:easypermissions:0.2.1'

    // Twitter SDK managed by Fabric.
    compile('com.twitter.sdk.android:twitter:2.3.1@aar') {
        transitive = true;
    }

    // Spotify API.
    compile(name: 'spotify-android-auth-1.0.0', ext: 'aar')
    compile(name: 'spotify-player-24-noconnect-2.20b', ext: 'aar')
    compile 'com.github.kaaes:spotify-web-api-android:0.4.1'

    // Cards and recycler adapter.
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'

    // For image loading.
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // ORM (Database management)
    compile 'org.greenrobot:greendao:3.2.0'

    compile 'com.android.support:support-v4:25.1.1'
    testCompile 'junit:junit:4.12'
}

greendao {
    schemaVersion 1
}